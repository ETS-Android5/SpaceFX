buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://sandec.bintray.com/repo"
        }
    }
    dependencies {
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.6.2'
        classpath 'org.javamodularity:moduleplugin:1.7.0'
        classpath 'org.beryx:badass-jlink-plugin:2.22.3'
        classpath 'com.sandec.jpro:jpro-plugin-gradle:2020.1.0'
    }
}

plugins {
    id 'java-library'
    id 'application'
    id 'com.google.osdetector' version '1.6.2'
    id 'org.javamodularity.moduleplugin' version '1.6.0'
    id 'org.beryx.jlink' version '2.16.4'
}

apply plugin: 'com.sandec.jpro'

group 'eu.hansolo.spacefx'
version '1.0-SNAPSHOT'

ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os

repositories {
    mavenCentral()
}

sourceCompatibility = 15
targetCompatibility = 15

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}
tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}
tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}

dependencies {
    implementation "org.openjfx:javafx-base:15:$platform"
    implementation "org.openjfx:javafx-graphics:15:$platform"
    implementation "org.openjfx:javafx-controls:15:$platform"
    implementation "org.openjfx:javafx-media:15:$platform"
    implementation "com.sandec.jpro:jpro-webapi:2020.1.0"
    implementation "one.jpro:one.jpro.sound:0.1.0"
}

test {
    jvmArgs = ['-Dglass.platform=Monocle', '-Dmonocle.platform=Headless']
}

mainClassName = '$moduleName/eu.hansolo.spacefx.SpaceFX'

// To run SpaceFX in the browser with jPro use:
task web() {
    mainClassName = "eu.hansolo.spacefx.SpaceFX"
    dependsOn jproRun
}

jar {
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect {  it.isDirectory() ? it : zipTree(it)  }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
        attributes ('Implementation-Title': 'SpaceFX',
                    'Class-Path': 'SpaceFX-1.0-SNAPSHOT.jar ',
                    'Main-Class': 'eu.hansolo.spacefx.Launcher')
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'eu.hansolo.spacefx'
    }
}

jpro {
    port             = 8080
    JVMArgs << '-Xmx1000m'
    visible          = false // Debug mode can also be used to create images via canvas and snapshot
    //noRenderJobs     = true // false to enable snapshot support
    jproVersion      = "2020.1.0"
    openURLOnStartup = false
}