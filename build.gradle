buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://sandec.bintray.com/repo"
        }
    }
    dependencies {
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.6.2'
        classpath 'org.javamodularity:moduleplugin:1.6.0'
        classpath 'org.beryx:badass-jlink-plugin:2.17.2'
        classpath 'com.sandec.jpro:jpro-plugin-gradle:2019.2.2'
    }
}

plugins {
    id 'java-library'
    id 'application'
    id 'com.google.osdetector' version '1.6.2'
    id 'org.javamodularity.moduleplugin' version '1.6.0'
    id 'org.beryx.jlink' version '2.17.2'
}

apply plugin: 'com.sandec.jpro'

group 'eu.hansolo.spacefx'
version '1.0-SNAPSHOT'

ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os

repositories {
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11

dependencies {
    implementation "org.openjfx:javafx-base:11:$platform"
    implementation "org.openjfx:javafx-graphics:11:$platform"
    implementation "org.openjfx:javafx-controls:11:$platform"
    implementation "org.openjfx:javafx-media:11:$platform"
    implementation "com.sandec.jpro:jpro-webapi:2019.2.2"
}

mainClassName = "$moduleName/eu.hansolo.spacefx.SpaceFX"

// To run SpaceFX in the browser with jPro use:
task web() {
    mainClassName = "eu.hansolo.spacefx.SpaceFX"
    dependsOn jproRun
}

jar {
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect {  it.isDirectory() ? it : zipTree(it)  }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
        attributes ('Implementation-Title': 'SpaceFX',
                    'Class-Path': 'SpaceFX-1.0-SNAPSHOT.jar ',
                    'Main-Class': 'eu.hansolo.spacefx.Launcher')
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'eu.hansolo.spacefx'
    }
}

jpro {
    port             = 8080
    JVMArgs << '-Xmx1000m'
    visible          = false // Debug mode can also be used to create images via canvas and snapshot
    noRenderJobs     = true // false to enable snapshot support
    jproVersion      = "2019.2.2"
    openURLOnStartup = false
}