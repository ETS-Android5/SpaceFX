buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.7.0'
        classpath 'org.javamodularity:moduleplugin:1.7.0'
        classpath 'org.beryx:badass-jlink-plugin:2.23.2'
    }
}

plugins {
    id 'java-library'
    id 'application'
    id 'com.google.osdetector' version '1.7.0'
    id 'org.javamodularity.moduleplugin' version '1.7.0'
    id 'org.beryx.jlink' version '2.23.2'
}

group 'eu.hansolo.spacefx'
version '1.0.0'

ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os

repositories {
    mavenCentral()
}

sourceCompatibility = 16
targetCompatibility = 16

dependencies {
    implementation "org.openjfx:javafx-base:16:$platform"
    implementation "org.openjfx:javafx-graphics:16:$platform"
    implementation "org.openjfx:javafx-controls:16:$platform"
    implementation "org.openjfx:javafx-media:16:$platform"
}

mainClassName = "$moduleName/eu.hansolo.spacefx.SpaceFX"

jar {
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect {  it.isDirectory() ? it : zipTree(it)  }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
        attributes ('Implementation-Title': 'SpaceFX',
                    'Class-Path': 'SpaceFX-1.0.0.jar ',
                    'Main-Class': 'eu.hansolo.spacefx.Launcher')
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'eu.hansolo.spacefx'
    }
}

// start the from gradle
task Main(type: JavaExec) {
    main = "eu.hansolo.spacefx.Launcher"
    classpath = sourceSets.main.runtimeClasspath
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}
tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}
tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}
